#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем мТекущийУзел;
Перем мСтруктураКэшДанных;

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Преобразовать текст в дерево.
Процедура ПреобразоватьТекстВДерево() Экспорт

	мСтруктураКэшДанных.Очистить();
	ВсегоСтрок = СтрЧислоСтрок(ЭтотОбъект.ТекстИсточник);

	ЭтоТекстВопроса = Ложь;
	УзелОтвета = Неопределено;

	ЭтотОбъект.ДеревоВопросов.Строки.Очистить();

	Для сч = 1 По ВсегоСтрок Цикл
		стр = СтрПолучитьСтроку(ЭтотОбъект.ТекстИсточник, сч);
		Если НЕ ЗначениеЗаполнено(стр) Тогда
			Продолжить;
		КонецЕсли;
		Описание = ОписаниеНачалаСтроки(Стр);

		Если Описание.ЭтоВопрос Тогда
			ЭтоТекстВопроса = Истина;
			УзелВопроса = ЭтотОбъект.ДеревоВопросов.Строки.Добавить();
			УзелВопроса.Текст = Описание.СтрокаВРезультат;
			УзелВопроса.Номер = Описание.Номер;

		ИначеЕсли Описание.ЭтоСтрока Тогда
			Если ЭтоТекстВопроса Тогда
				УзелВопроса.Текст = УзелВопроса.Текст + Символы.ПС + Описание.СтрокаВРезультат;
			Иначе
				УзелОтвета.Текст = УзелОтвета.Текст + Символы.ПС + Описание.СтрокаВРезультат;
			КонецЕсли;

		ИначеЕсли Описание.ЭтоОтвет Тогда
			ЭтоТекстВопроса = Ложь;

			УзелОтвета = УзелВопроса.Строки.Добавить();
			УзелОтвета.Текст = Описание.СтрокаВРезультат;
			УзелОтвета.Номер = Описание.Номер;
		КонецЕсли;

	КонецЦикла;

	ПроставитьФлагЗапретаПеремешивания();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПреобразованиеИсходногоТекста

Процедура ПроставитьФлагЗапретаПеремешивания()

	Указатели = Новый Массив;
	Указатели.Добавить("верны ответы");
	Указатели.Добавить("верные ответы");
	Указатели.Добавить("верны варианты");
	Указатели.Добавить("верны все варианты");
	Указатели.Добавить("верны все ответы");
	Указатели.Добавить("с помощью любого из вариантов");
	Указатели.Добавить("правильные варианты");
	Указатели.Добавить("реально выполнимы");
	Указатели.Добавить("должны выполняться условия");
	Указатели.Добавить("наличествуют свойства");
	Указатели.Добавить("возможно развитие события по варианту");

	Для каждого УзелВопрос Из ЭтотОбъект.ДеревоВопросов.Строки Цикл

		Для сч = -УзелВопрос.Строки.Количество() По - 1 Цикл
			инд = -сч - 1;
			УзелОтвет = УзелВопрос.Строки.Получить(инд);

			Прерывать = Ложь;
			Для каждого Указатель Из Указатели Цикл
				Если СтрНайти(нРег(УзелОтвет.Текст), Указатель) Тогда
					УзелВопрос.ЗапретПеремешивания = Истина;
					Прерывать = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;

			Если Прерывать Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Функция ОписаниеНачалаСтроки(стр)

	Если НЕ мСтруктураКэшДанных.Свойство("ОписанияСтрок") Тогда
		мСтруктураКэшДанных.Вставить("ОписанияСтрок", Новый Соответствие);

	КонецЕсли;

	Результат = мСтруктураКэшДанных.ОписанияСтрок.Получить(стр);
	Если Результат = Неопределено Тогда
		Результат = Новый Структура;
		Результат.Вставить("НачинаетсяСНомера", Ложь);
		Результат.Вставить("ВНомереДваРазряда", Ложь);
		Результат.Вставить("Номер");
		Результат.Вставить("ЭтоВопрос", Ложь);
		Результат.Вставить("ЭтоОтвет", Ложь);
		Результат.Вставить("ЭтоСтрока", Ложь);
		Результат.Вставить("СтрокаВРезультат", "");

		СтрСимволы = "0987654321.";
		Слово = "";
		Для сч = 1 По СтрДлина(стр) Цикл
			сим = Сред(стр, сч, 1);
			Если НЕ СтрНайти(СтрСимволы, Сим) Тогда
				Прервать;
			КонецЕсли;

			Слово = Слово + Сим;
		КонецЦикла;

		ЧастиСтроки = РазбитьСтроку(Слово, ".");

		Результат.НачинаетсяСНомера = ЧастиСтроки.Разбито И ЗначениеЗаполнено(ЧастиСтроки.Левая);
		Результат.ВНомереДваРазряда = ЧастиСтроки.Разбито И ЗначениеЗаполнено(ЧастиСтроки.Правая);
		Результат.Номер = Слово;

		Результат.ЭтоВопрос = Результат.НачинаетсяСНомера
			И Результат.ВНомереДваРазряда;

		Результат.ЭтоОтвет = Результат.НачинаетсяСНомера
			И НЕ Результат.ВНомереДваРазряда;

		Результат.ЭтоСтрока = НЕ (Результат.ЭтоВопрос ИЛИ Результат.ЭтоОтвет);

		СтрокаВРезультат = Стр;
		Если НЕ Результат.ЭтоСтрока Тогда
			СтрокаВРезультат = Сред(СтрокаВРезультат, СтрДлина(Слово) + 1);
		КонецЕсли;
		СтрокаВРезультат = СокрЛП(СтрокаВРезультат);

		Результат.СтрокаВРезультат = СтрокаВРезультат;

		мСтруктураКэшДанных.ОписанияСтрок.Вставить(стр, Результат);
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ЗагрузкаВопросов

#КонецОбласти

#КонецОбласти

#Область Инициализация

мСтруктураКэшДанных = Новый Структура;

#КонецОбласти

#КонецЕсли
