
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИнициализироватьКомпоновщик();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ТекущийОбъект.НастройкиСхемы = Новый ХранилищеЗначения(Настройки);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВывестиОтбор(Команда)
	ВывестиОтборНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьКомпоновщик()

	Схема = Справочники.ОтборыВопросов.ПолучитьМакет("СхемаОтбораВопросов");
	Адрес = ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор);
	ИДН = Новый ИсточникДоступныхНастроекКомпоновкиДанных(Адрес);
	КомпоновщикНастроек.Инициализировать(ИДН);

	Если РаботаСДиалогамиКлиентСервер.ЭтоНовый(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	Настройки = ТекОбъект.НастройкиСхемы.Получить();
	Если Настройки = Неопределено Тогда
		Возврат;
	КонецЕсли;

	КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(КомпоновщикНастроек.Настройки.Отбор, Настройки.Отбор);

КонецПроцедуры

&НаСервере
Процедура ВывестиОтборНаСервере()

	Схема = Справочники.ОтборыВопросов.ПолучитьМакет("СхемаОтбораВопросов");
	Настройки = Схема.НастройкиПоУмолчанию;
	НастройкиТекущие = КомпоновщикНастроек.ПолучитьНастройки();

	КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(Настройки.Отбор,  НастройкиТекущие.Отбор);

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(Схема, Настройки);

	ПроцКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцКомпоновки.Инициализировать(Макет);

	ДокументРезультат.Очистить();
	ПроцВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцВывода.УстановитьДокумент(ДокументРезультат);
	ПроцВывода.Вывести(ПроцКомпоновки);

КонецПроцедуры

#КонецОбласти
