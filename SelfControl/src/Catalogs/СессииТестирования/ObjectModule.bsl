#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;		
	КонецЕсли; 
	
	СгенерироватьНаименование();
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	ЭтотОбъект.ДатаНачала = ТекущаяДатаСеанса();
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ОтборВопросов) Тогда
		ЗаполнитьВопросы();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура СгенерироватьНаименование()
	НаименованиеОтбора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.ОтборВопросов, "Наименование");
	
	Если ЭтотОбъект.Завершена Тогда
		ШаблонНаименования = "%1 (%2 - %3), %4";
		
		ВремяТестированияСек = ЭтотОбъект.ДатаОкончания - ЭтотОбъект.ДатаНачала;
		ВремяТестирования = ОбщегоНазначения.ПредставлениеВремени(ВремяТестированияСек);
		
		ЭтотОбъект.Наименование  = СтрШаблон(ШаблонНаименования
			, НаименованиеОтбора
			, ОбщегоНазначенияКлиентСервер.ФДата(ЭтотОбъект.ДатаНачала)
			, ОбщегоНазначенияКлиентСервер.ФДата(ЭтотОбъект.ДатаОкончания)
			, ВремяТестирования);
	Иначе
		ШаблонНаименования = "%1: %2";		
		ЭтотОбъект.Наименование = СтрШаблон(ШаблонНаименования
			, НаименованиеОтбора
			, ЭтотОбъект.ДатаНачала);
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьВопросы()
	СписокВопросовОтбора = Справочники.ОтборыВопросов.СписокВопросовОтбора(ЭтотОбъект.ОтборВопросов);
	ПеремешиватьВопросы = Константы.ПеремешиватьВопросы.Получить();	
	Если ПеремешиватьВопросы Тогда
		СписокВопросовВРезультат = ОбщегоНазначения.ПеремешатьЗначения(СписокВопросовОтбора);				
	Иначе
		СписокВопросовВРезультат = СписокВопросовОтбора;						
	КонецЕсли;	
	
	ЭтотОбъект.Вопросы.Очистить();
	Для каждого Вопрос Из СписокВопросовВРезультат Цикл
		ЭтотОбъект.Вопросы.Добавить().Вопрос = Вопрос;		
	КонецЦикла; 
	
КонецПроцедуры
 
#КонецОбласти 

#КонецЕсли
