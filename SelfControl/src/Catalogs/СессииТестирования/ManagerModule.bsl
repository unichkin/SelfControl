#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция НоваяСессия(ОтборВопросов) Экспорт
	Результат = Справочники.СессииТестирования.СоздатьЭлемент();
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ОтборВопросов", ОтборВопросов);
	Результат.Заполнить(ДанныеЗаполнения);
	Результат.Записать();
	Возврат Результат.Ссылка;
КонецФункции

Функция НайтиПоследнююАктивную(ОтборВопросов) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
		|	СессииТестирования.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СессииТестирования КАК СессииТестирования
		|ГДЕ
		|	СессииТестирования.ОтборВопросов = &ОтборВопросов
		|	И СессииТестирования.Завершена = ЛОЖЬ
		|
		|УПОРЯДОЧИТЬ ПО
		|	СессииТестирования.ДатаНачала";

	Запрос.УстановитьПараметр("ОтборВопросов", ОтборВопросов);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Результат = Справочники.СессииТестирования.ПустаяСсылка();
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();

		Результат = Выборка.Ссылка;
	КонецЕсли;

	Возврат Результат;

КонецФункции

Процедура ЗаписатьСостояние(Сессия, Вопрос, Проверен, ЭтоВерныйОтвет) Экспорт

	СессияОбъект = Сессия.ПолучитьОбъект();

	СтрокаТЧ = СессияОбъект.Вопросы.Найти(Вопрос, "Вопрос");
	СтрокаТЧ.Проверен = Проверен;
	СтрокаТЧ.ЭтоВерныйОтвет = ЭтоВерныйОтвет;
	СтрокаТЧ.ВремяОтвета = ТекущаяДатаСеанса();
	СтрокаТЧ.ДлительностьОтвета = СтрокаТЧ.ВремяОтвета - СтрокаТЧ.ВремяПросмотра;

	ВсеВопросыПроверены = СессияОбъект.Вопросы.Итог("Проверен") = СессияОбъект.Вопросы.Количество();
	Если ВсеВопросыПроверены Тогда
		ЗавершитьОбъектСессии(СессияОбъект);
	Иначе
		СессияОбъект.Записать();
	КонецЕсли;

КонецПроцедуры

Процедура ЗавершитьСессию(Сессия) Экспорт
	СессияОбъект = Сессия.ПолучитьОбъект();
	ЗавершитьОбъектСессии(СессияОбъект);
КонецПроцедуры

Процедура ЗаписатьВремяПросмотра(Сессия, Вопрос) Экспорт
	СессияОбъект = Сессия.ПолучитьОбъект();

	Если НЕ СессияОбъект.Стартована Тогда
		СессияОбъект.Стартована = Истина;
	КонецЕсли;

	СтрокаТЧ = СессияОбъект.Вопросы.Найти(Вопрос, "Вопрос");
	СтрокаТЧ.ВремяПросмотра = ТекущаяДатаСеанса();
	СессияОбъект.Записать();
КонецПроцедуры

Процедура ЗаписатьПросмотрПояснения(Сессия, Вопрос) Экспорт

	СессияОбъект = Сессия.ПолучитьОбъект();
	СтрокаТЧ = СессияОбъект.Вопросы.Найти(Вопрос, "Вопрос");
	СтрокаТЧ.ПояснениеПросмотрено = Истина;

	СессияОбъект.ОбменДанными.Загрузка = Истина;
	СессияОбъект.Записать();

КонецПроцедуры

Процедура ПеремешатьВопросы(Сессия) Экспорт

	СессияОбъект = Сессия.ПолучитьОбъект();
	ТаблицаВопросов = СессияОбъект.Вопросы.Выгрузить();
	СписокСтрок = Новый Массив;
	Для каждого СтрокаТаблицы Из ТаблицаВопросов Цикл
		СписокСтрок.Добавить(СтрокаТаблицы);
	КонецЦикла;

	СписокСтрок = ОбщегоНазначения.ПеремешатьЗначения(СписокСтрок);
	СессияОбъект.Вопросы.Очистить();
	Для каждого СтрокаТаблицы Из СписокСтрок Цикл
		СтрокаВопрос = СессияОбъект.Вопросы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаВопрос, СтрокаТаблицы);
	КонецЦикла;
	СессияОбъект.Записать();

КонецПроцедуры

Процедура СортироватьВопросы(Сессия) Экспорт

	СессияОбъект = Сессия.ПолучитьОбъект();
	ТаблицаВопросов = СессияОбъект.Вопросы.Выгрузить();
	СписокВопросов = ТаблицаВопросов.ВыгрузитьКолонку("Вопрос");
	РеквизитыВопросов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(СписокВопросов, "РеквизитДопУпорядочивания");
	ТаблицаВопросов.Колонки.Добавить("Порядок");
	Для каждого СтрокаТаблицы Из ТаблицаВопросов Цикл
		РеквизитыВопроса = РеквизитыВопросов.Получить(СтрокаТаблицы.Вопрос);
		СтрокаТаблицы.Порядок = РеквизитыВопроса.РеквизитДопУпорядочивания;
	КонецЦикла;
	ТаблицаВопросов.Сортировать("Порядок");
	ТаблицаВопросов.Колонки.Удалить("Порядок");
	СессияОбъект.Вопросы.Загрузить(ТаблицаВопросов);
	СессияОбъект.Записать();

КонецПроцедуры

Функция ВопросПринадлежитСессии(Сессия, Вопрос) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СессииТестированияВопросы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СессииТестирования.Вопросы КАК СессииТестированияВопросы
		|ГДЕ
		|	СессииТестированияВопросы.Ссылка = &Сессия
		|	И СессииТестированияВопросы.Вопрос = &Вопрос
		|	И СессииТестированияВопросы.Ссылка.Стартована = ИСТИНА";

	Запрос.УстановитьПараметр("Вопрос", Вопрос);
	Запрос.УстановитьПараметр("Сессия", Сессия);

	РезультатЗапроса = Запрос.Выполнить();
	Результат = Не РезультатЗапроса.Пустой();
	Возврат Результат;
КонецФункции

Процедура ВывестиРезультатТестирования(Сессия, ДокументРезультат) Экспорт

	Схема = Справочники.СессииТестирования.ПолучитьМакет("РезультатСессии");

	НастройкиСхемы = Схема.НастройкиПоУмолчанию;
	НастройкиСхемы.ПараметрыДанных.УстановитьЗначениеПараметра("Сессия", Сессия);
	НастройкиСхемы.ПараметрыВывода.УстановитьЗначениеПараметра("Заголовок", "" + Сессия);
	НастройкиСхемы.ПараметрыДанных.УстановитьЗначениеПараметра("ОбщийРезультат", "TO DO: сделать итоги");

	КомпМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпМакета.Выполнить(Схема, НастройкиСхемы);

	ПроцКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);

	ПроцВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ДокументРезультат.Очистить();
	ПроцВывода.УстановитьДокумент(ДокументРезультат);

	ПроцВывода.Вывести(ПроцКомпоновки);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗавершитьОбъектСессии(СессияОбъект)
	СессияОбъект.ДатаОкончания = ТекущаяДатаСеанса();
	СессияОбъект.Завершена = Истина;
	СессияОбъект.Записать();
КонецПроцедуры

#КонецОбласти

#КонецЕсли
