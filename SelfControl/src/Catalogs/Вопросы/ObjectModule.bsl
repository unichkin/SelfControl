
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Справочники.Вопросы.УстановитьФлагиСостоянияВопроса(ЭтотОбъект);
	Если НЕ ЭтотОбъект.ЭтоГруппа Тогда
		ЭтотОбъект.Наименование = ЭтотОбъект.ТекстВопроса;
	КонецЕсли;
	ОбработатьКартинкиПояснения();
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Если ЭтотОбъект.ЭтоНовый() Тогда
		Если ЭтотОбъект.ЭтоГруппа Тогда
			Возврат;
		КонецЕсли;
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
		НастройкаПорядкаЭлементов.ЗаполнитьЗначениеРеквизитаУпорядочивания(ЭтотОбъект, Ложь);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьКартинкиПояснения()

	Если ЭтотОбъект.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	ФорматДокумент = ЭтотОбъект.Пояснение.Получить();
	Если ФорматДокумент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для каждого Закладка Из ФорматДокумент.Элементы Цикл
		Для каждого Элем Из Закладка.Элементы Цикл
			Если ТипЗнч(Элем) = Тип("КартинкаФорматированногоДокумента") Тогда
				Если ТипЗнч(Элем.Картинка) = Тип("Строка") Тогда
					Адрес = РаскодироватьСтроку(Элем.Картинка, СпособКодированияСтроки.КодировкаURL);
					Адрес = СтрЗаменить(Адрес, "file:///", "");
					Файл = Новый Файл(Адрес);
					Если Файл.Существует() Тогда
						Элем.Картинка = Новый Картинка(Адрес);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	ЭтотОбъект.Пояснение = Новый ХранилищеЗначения(ФорматДокумент);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
