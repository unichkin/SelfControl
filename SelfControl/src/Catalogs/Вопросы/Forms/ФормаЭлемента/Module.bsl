
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьЗаголовок();
	УстановитьЦветПроработан(ЭтотОбъект);
	НастройкиОформленияКлиентСервер.ОформитьВопрос(Пояснение);
	Элементы.РезультатПроверкиОрфографии.Видимость = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЭтоНовый(ЭтотОбъект) Тогда
		Если ЗначениеЗаполнено(Пояснение.Элементы) Тогда
			УстановитьПросмотрПояснения(Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если РаботаСДиалогамиКлиентСервер.ФорматДокументЗаполнен(Пояснение) Тогда
		ТекущийОбъект.Пояснение = Новый ХранилищеЗначения(Пояснение);
	Иначе
		ТекущийОбъект.Пояснение = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;

	Если ЭтоНовый(ЭтотОбъект) Тогда
		ТекущийОбъект.РеквизитДопУпорядочивания = 0;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Пояснение = ТекущийОбъект.Пояснение.Получить();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить(РаботаСДиалогамиКлиент.СобытияОповещений().ЗаписьВопроса
		,
		, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПорядковыйНомерПриИзменении(Элемент)
	УстановитьЗаголовок();
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	УстановитьЗаголовок();
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеПриИзменении(Элемент)
	НастройкиОформленияКлиентСервер.ОформитьВопрос(Пояснение);

КонецПроцедуры

&НаКлиенте
Процедура Проработан(Команда)
	Объект.Проработан = НЕ Объект.Проработан;
	УстановитьЦветПроработан(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеДобавитьИсточник(Команда)

	Оповещение = Новый ОписаниеОповещения("ДобавлениеИсточникаПоясненияЗавершение", ЭтотОбъект);
	ПоказатьВводСтроки(Оповещение, , "Введите ссылку источника:");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПояснениеТолькоПросмотр(Команда)
	УстановитьПросмотрПояснения();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОрфографию(Команда)
	РезПроверки = РаботаСВопросамиВызовСервера.ПроверитьОрфографию(Объект);
	Элементы.РезультатПроверкиОрфографии.Видимость = РезПроверки.ЕстьОшибка;
	РезультатПроверкиОрфографии = РезПроверки.Текст;
	Если НЕ РезПроверки.ЕстьОшибка Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Проверка орфографии: ошибок нет!");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Сервис

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНовый(Форма)
	Возврат НЕ ЗначениеЗаполнено(Форма.Объект.Ссылка);
КонецФункции

#КонецОбласти

#Область Диалог

#Область НастройкиФормы

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЦветПроработан(Форма)

	Цвет = ?(Форма.Объект.Проработан, WebЦвета.СветлоЗеленый, WebЦвета.Желтый);
	Форма.Элементы.ФормаПроработан.ЦветФона = Цвет;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПросмотрПояснения(Флаг = Неопределено)
	Если Флаг = Неопределено Тогда
		Флаг = НЕ Элементы.ПояснениеТолькоПросмотр.Пометка;
	КонецЕсли;
	Элементы.ПояснениеТолькоПросмотр.Пометка = Флаг;
	Элементы.Пояснение.ТолькоПросмотр = Флаг;
	Элементы.ГруппаКомандыПояснения.Доступность = НЕ Флаг;
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок()
	ННРодителя = "";
	Если ЗначениеЗаполнено(Объект.Родитель) Тогда
		ННРодителя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Родитель, "РеквизитДопУпорядочивания");
	КонецЕсли;
	Если ЗначениеЗаполнено(ННРодителя) Тогда
		ННРодителя = "" + ННРодителя + ".";
	КонецЕсли;

	Заголовок = "Вопрос №" + ННРодителя + Объект.РеквизитДопУпорядочивания;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОповещений

&НаКлиенте
Процедура ДобавлениеИсточникаПоясненияЗавершение(Результат, ДопПараметры) Экспорт

	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;

	ТекстПояснения = Пояснение.ПолучитьТекст();
	ДобавитьЗаголовокИсточников = СтрНайти(ТекстПояснения, "Источник") = 0;
	Если ДобавитьЗаголовокИсточников Тогда
		Параграф = Пояснение.Элементы.Добавить(Тип("ПараграфФорматированногоДокумента"));
		Параграф.Элементы.Добавить("Источники: ", Тип("ТекстФорматированногоДокумента"));
	КонецЕсли;

	ПараграфСсылки = Пояснение.Элементы.Добавить(Тип("ПараграфФорматированногоДокумента"));
	Элем = ПараграфСсылки.Элементы.Добавить(Результат, Тип("ТекстФорматированногоДокумента"));
	ЭтоСсылка = СтрНачинаетсяС(нРег(Результат), "http");
	Если ЭтоСсылка Тогда
		Элем.НавигационнаяСсылка = Результат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти
