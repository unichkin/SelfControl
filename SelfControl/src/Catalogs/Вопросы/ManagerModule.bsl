#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сериализовать.
//
// Параметры:
//  Вопрос - СправочникСсылка.Вопросы
//
// Возвращаемое значение:
//  Строка
//
Функция Сериализовать(Вопрос) Экспорт

	Результат = "";

	ДополнитьРезультат(Результат, Вопрос.ТекстВопроса);
	Для каждого СтрокаОтвет Из Вопрос.Ответы Цикл
		ДопТекст = ?(СтрокаОтвет.ЭтоВерныйОтвет, "*", "");
		ДополнитьРезультат(Результат, СтрокаОтвет.ТекстОтвета, ДопТекст);
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Номер верного ответа.
//
// Параметры:
//  Вопрос - СправочникСсылка.Вопросы
//
// Возвращаемое значение:
//  Число
//
Функция НомерВерногоОтвета(Вопрос) Экспорт

	СтрокаТЧ = Вопрос.Ответы.Найти(Истина, "ЭтоВерныйОтвет");
	Если СтрокаТЧ = Неопределено Тогда
		Результат = 0;
	Иначе
		Результат = Вопрос.Ответы.Индекс(СтрокаТЧ) + 1;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Установка значений реквизитов "ЕстьОтвет", "ЕстьПояснение"
//
// Параметры:
//  Вопрос - СправочникОбъект.Вопросы
//
Процедура УстановитьФлагиСостоянияВопроса(Вопрос) Экспорт

	Если Вопрос.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;

	Вопрос.ЕстьОтвет = Вопрос.Ответы.Найти(Истина, "ЭтоВерныйОтвет") <> Неопределено;

	Пояснение = Вопрос.Пояснение.Получить();
	Вопрос.ЕстьПояснение = РаботаСДиалогамиКлиентСервер.ФорматДокументЗаполнен(Пояснение);
	Если НЕ Вопрос.ЕстьПояснение Тогда
		Вопрос.Пояснение = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьРезультат(Результат, Текст, ДопТекст = "")

	ТекстВРезультат = ДопТекст + ТекстВСериализованныйРезультат(Текст);
	Разделитель = ?(ЗначениеЗаполнено(Результат), Символы.ПС, "");
	Результат = Результат + Разделитель + ТекстВРезультат;

КонецПроцедуры

Функция ТекстВСериализованныйРезультат(Текст)

	Результат = Текст;
	Если ЭтоМногострочныйТекст(Текст) Тогда
		Результат = """" + Текст + """";

	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ЭтоМногострочныйТекст(Текст)
	Возврат СтрЧислоСтрок(Текст) > 1;

КонецФункции

#КонецОбласти

#КонецЕсли
