
#Область ПрограммныйИнтерфейс

// Проверка орфографии.
//
// Параметры:
//  Текст - Строка
//
// Возвращаемое значение:
//  см. ОбщегоНазначенияКлиентСервер.НовыйРезультатПроверки
//
Функция ПроверкаОрфографии(Знач Текст) Экспорт

	Результат = ОбщегоНазначенияКлиентСервер.НовыйРезультатПроверки();
	Результат.Вставить("ОшибочныеСлова", Новый Массив);

	Если ЗначениеЗаполнено(Текст) Тогда
		СтрокаЗапроса = "services/spellservice/checkText?text=" + Текст;
		СоединениеHTTP = Новый HTTPСоединение("speller.yandex.net", , , , , 5);
		ЗапросHTTP = Новый HTTPЗапрос(СтрокаЗапроса);
		ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP);

		СтрокаXML = ОтветHTTP.ПолучитьТелоКакСтроку();
		ОшибочныеСлова = ОшибочныеСловаВРезультатеПроверкиОрфографии(СтрокаXML);
		Если ЗначениеЗаполнено(ОшибочныеСлова) Тогда
			Результат.ЕстьОшибка = Истина;
			Результат.Текст = СтрСоединить(ОшибочныеСлова, ", ");
			Результат.ОшибочныеСлова = ОшибочныеСлова;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОшибочныеСловаВРезультатеПроверкиОрфографии(СтрокаXML)

	ОшибочныеСлова = Новый Массив;

	СловаИсключенияПриПроверкеОрфографии = НастройкиПрограммыКлиентСервер.СловаИсключенияПриПроверкеОрфографии();

	СтекУзлов = Новый Массив;
	ТекущийПуть = "";

	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	Пока ЧтениеXML.Прочитать() Цикл

		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			СтекУзлов.Добавить(ЧтениеXML.Имя);
			ТекущийПуть = СтрСоединить(СтекУзлов, "\");

		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			Если ТекущийПуть = "SpellResult\error\word" Тогда
				Слово = ЧтениеXML.Значение;
				Если СтрНайти(СловаИсключенияПриПроверкеОрфографии, Врег(Слово)) > 0 Тогда
					Продолжить;
				КонецЕсли;
				ОшибочныеСлова.Добавить(Слово);
			КонецЕсли;

		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			СтекУзлов.Удалить(СтекУзлов.ВГраница());
			ТекущийПуть = СтрСоединить(СтекУзлов, "\");

		КонецЕсли;

	КонецЦикла;

	Возврат ОшибочныеСлова;

КонецФункции

#КонецОбласти
