////////////////////////////////////////////////////////////////////////////////
// Конфигурация "Самоконтроль"
// Авторское право (с) 2021, Уничкин Р. А.

// Разрешается повторное распространение и использование как в виде исходника так и в двоичной форме,
// с модификациями или без, при соблюдении следующих условий:
// - При повторном распространении исходного кода должно оставаться указанное выше уведомление об авторском
//   праве, этот список условий и нижеследующий отказ от гарантий.
// - При повторном распространении двоичного кода должно воспроизводиться указанное выше уведомление об
//   авторском праве, этот список условий и нижеследующий отказ от гарантий в документации и/или в других
//   материалах, поставляемых при распространении.
//
// ЭТО ПРОГРАММА ПРЕДОСТАВЛЕНА БЕСПЛАТНО ДЕРЖАТЕЛЯМИ АВТОРСКИХ ПРАВ И/ИЛИ ДРУГИМИ СТОРОНАМИ "КАК ОНА ЕСТЬ"
// БЕЗ КАКОГО-ЛИБО ВИДА ГАРАНТИЙ, ВЫРАЖЕННЫХ ЯВНО ИЛИ ПОДРАЗУМЕВАЕМЫХ, ВКЛЮЧАЯ, НО НЕ ОГРАНИЧИВАЯСЬ ИМИ,
// ПОДРАЗУМЕВАЕМЫЕ ГАРАНТИИ КОММЕРЧЕСКОЙ ЦЕННОСТИ И ПРИГОДНОСТИ ДЛЯ КОНКРЕТНОЙ ЦЕЛИ. НИ В КОЕМ СЛУЧАЕ,
// ЕСЛИ НЕ ТРЕБУЕТСЯ СООТВЕТСТВУЮЩИМ ЗАКОНОМ, ИЛИ НЕ УСТАНОВЛЕНО В УСТНОЙ ФОРМЕ, НИ ОДИН ДЕРЖАТЕЛЬ АВТОРСКИХ
// ПРАВ И НИ ОДНО ДРУГОЕ ЛИЦО, КОТОРОЕ МОЖЕТ ИЗМЕНЯТЬ И/ИЛИ ПОВТОРНО РАСПРОСТРАНЯТЬ ПРОГРАММУ, КАК БЫЛО
// РАЗРЕШЕНО ВЫШЕ, НЕ ОТВЕТСТВЕННЫ ПЕРЕД ВАМИ ЗА УБЫТКИ, ВКЛЮЧАЯ ЛЮБЫЕ ОБЩИЕ, СЛУЧАЙНЫЕ, СПЕЦИАЛЬНЫЕ ИЛИ
// ПОСЛЕДОВАВШИЕ УБЫТКИ, ПРОИСТЕКАЮЩИЕ ИЗ ИСПОЛЬЗОВАНИЯ ИЛИ НЕВОЗМОЖНОСТИ ИСПОЛЬЗОВАНИЯ ПРОГРАММЫ (ВКЛЮЧАЯ,
// НО НЕ ОГРАНИЧИВАЯСЬ ПОТЕРЕЙ ДАННЫХ, ИЛИ ДАННЫМИ, СТАВШИМИ НЕПРАВИЛЬНЫМИ, ИЛИ ПОТЕРЯМИ ПРИНЕСЕННЫМИ ИЗ-ЗА
// ВАС ИЛИ ТРЕТЬИХ ЛИЦ, ИЛИ ОТКАЗОМ ПРОГРАММЫ РАБОТАТЬ СОВМЕСТНО С ДРУГИМИ ПРОГРАММАМИ), ДАЖЕ ЕСЛИ ТАКОЙ
// ДЕРЖАТЕЛЬ ИЛИ ДРУГОЕ ЛИЦО БЫЛИ ИЗВЕЩЕНЫ О ВОЗМОЖНОСТИ ТАКИХ УБЫТКОВ.

#Область ПрограммныйИнтерфейс

// Упрощенный конструктор описания типа
//
// Параметры:
//  ИмяТипа - Строка, Массив элементов типа "Тип" - первый параметр конструктора ОписаниеТипов()
//  п1 - Произвольный, первый параметр квалификатора
//  п2 - Произвольный, второй параметр квалификатора
//  п3 - Произвольный, третий параметр квалификатора
//
// Возвращаемое значение:
//  ОписаниеТипов
//
Функция ОписаниеТипа(ИмяТипа, п1 = Неопределено, п2 = Неопределено, п3 = Неопределено) Экспорт

    Перем Результат;

    Если ИмяТипа = "Строка" Тогда

        п1 = ?(п1 = Неопределено, 0, п1);
        Если п2 = Неопределено Тогда
            п2 = ДопустимаяДлина.Переменная;
        КонецЕсли;

        КвалСтроки = Новый КвалификаторыСтроки(п1, п2);
        Результат = Новый ОписаниеТипов(ИмяТипа, , , , КвалСтроки);

    ИначеЕсли ИмяТипа = "Число" Тогда

        п1 = ?(п1 = Неопределено, 0, п1);
        п2 = ?(п2 = Неопределено, 0, п2);
        Если п3 = Неопределено Тогда
            п3 = ДопустимыйЗнак.Любой;
        КонецЕсли;

        КвалЧисла = Новый КвалификаторыЧисла(п1, п2, п3);
        Результат = Новый ОписаниеТипов(ИмяТипа, , , КвалЧисла);

    ИначеЕсли ИмяТипа = "Дата" Тогда

        Если п1 = Неопределено Тогда
            п1 = ЧастиДаты.ДатаВремя;
        КонецЕсли;

        КвалДаты = Новый КвалификаторыДаты(п1);
        Результат = Новый ОписаниеТипов(ИмяТипа, , , , , КвалДаты);

    Иначе
        Результат = Новый ОписаниеТипов(ИмяТипа);
    КонецЕсли;

    Возврат Результат;

КонецФункции

// Универсальная функция для парсинга строк
//
// Параметры:
//  ИсходнаяСтрока - Строка
//  СтрокаРазделитель - Строка
//  ОбрезатьПробелы - Булево
//
// Возвращаемое значение:
//  СтруктураЧастиСтроки - Структура
//   * Левая - Строка - Левая часть строки (до начала СтрокаРазделитель)
//   * Правая - Строка - Правая часть строки (после окончания СтрокаРазделитель)
//   * Разбито - Булево - флаг указывающий что в исходной строке есть СтрокаРазделитель
//
Функция РазбитьСтроку(ИсходнаяСтрока, СтрокаРазделитель, ОбрезатьПробелы = Истина) Экспорт

    поз = СтрНайти(ИсходнаяСтрока, СтрокаРазделитель);
    ДлинаРазделителя = СтрДлина(СтрокаРазделитель);
    Разбито = поз > 0;
    ЛеваяЧастьСтроки = Лев(ИсходнаяСтрока, поз - 1);
    Если ОбрезатьПробелы Тогда
        ЛеваяЧастьСтроки = СокрЛП(ЛеваяЧастьСтроки);
    КонецЕсли;

    ПраваяЧастьСтроки = Сред(ИсходнаяСтрока, поз + ДлинаРазделителя);
    Если ОбрезатьПробелы Тогда
        ПраваяЧастьСтроки = СокрЛП(ПраваяЧастьСтроки);
    КонецЕсли;

    СтруктураЧастиСтроки = Новый Структура("Левая, Правая, Разбито"
    , ЛеваяЧастьСтроки
    , ПраваяЧастьСтроки
    , Разбито);

    Возврат СтруктураЧастиСтроки;

КонецФункции

#КонецОбласти
