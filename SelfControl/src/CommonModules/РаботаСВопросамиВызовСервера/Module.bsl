#Область ПрограммныйИнтерфейс

Функция ПолныйНомерВопроса(Знач Вопрос) Экспорт

	Источник = Вопрос;

	Номера = Новый Массив;

	Пока Истина Цикл
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Вопросы.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
			|	Вопросы.Родитель КАК Родитель,
			|	Вопросы.ЭтоГруппаТестов КАК ЭтоГруппаТестов,
			|	Вопросы.ЭтоГруппа КАК ЭтоГруппа
			|ИЗ
			|	Справочник.Вопросы КАК Вопросы
			|ГДЕ
			|	Вопросы.Ссылка = &Вопрос";

		Запрос.УстановитьПараметр("Вопрос", Источник);

		РезультатЗапроса = Запрос.Выполнить();

		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();

		Если Выборка.РеквизитДопУпорядочивания = Неопределено Тогда
			Прервать;
		КонецЕсли;

		Источник = Выборка.Родитель;
		Если Выборка.ЭтоГруппа И НЕ Выборка.ЭтоГруппаТестов Тогда
			Продолжить;
		КонецЕсли;

		Номера.Вставить(0, Выборка.РеквизитДопУпорядочивания);

	КонецЦикла;

	Возврат СтрСоединить(Номера, ".");

КонецФункции

Функция ПроверитьОрфографию(Знач ОбъектВопроса) Экспорт
	Результат = ОбщегоНазначенияКлиентСервер.НовыйРезультатПроверки();
	РезультатПроверкиТекста = ПроверкаОрфографииВызовСервера.ПроверкаОрфографии(ОбъектВопроса.ТекстВопроса);

	ОбщегоНазначенияКлиентСервер.ДобавитьРезультатПроверки(Результат
		, РезультатПроверкиТекста
		, "Ошибки при проверке текста вопроса: " + Символы.ПС);

	Для каждого СтрокаТЧ Из ОбъектВопроса.Ответы Цикл
		РезультатПроверкиОтвета = ПроверкаОрфографииВызовСервера.ПроверкаОрфографии(СтрокаТЧ.ТекстОтвета);

		ОбщегоНазначенияКлиентСервер.ДобавитьРезультатПроверки(Результат
			, РезультатПроверкиОтвета
			, "Ошибки при проверке ответа №"+ СтрокаТЧ.НомерСтроки +": " + Символы.ПС);
	КонецЦикла;

	Возврат Результат;

КонецФункции

#КонецОбласти
