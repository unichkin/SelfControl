#Область ПрограммныйИнтерфейс

// Формат документ заполнен.
//
// Параметры:
//  ФорматДокумент - ФорматированныйДокумент
//
// Возвращаемое значение:
//  Булево
//
Функция ФорматДокументЗаполнен(ФорматДокумент) Экспорт

	Результат = Ложь;
	Если ФорматДокумент <> Неопределено Тогда
		Для Каждого Параграф Из ФорматДокумент.Элементы Цикл
			Для Каждого ЭлемПараграфа Из Параграф.Элементы Цикл
				ТипЭлементаПараграфа = ТипЗнч(ЭлемПараграфа);
				Если ТипЭлементаПараграфа = Тип("ТекстФорматированногоДокумента") Тогда
					Результат = ЗначениеЗаполнено(ЭлемПараграфа.Текст);
				ИначеЕсли ТипЭлементаПараграфа = Тип("КартинкаФорматированногоДокумента") Тогда
					 Результат = Истина;

				КонецЕсли;

				Если Результат Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Результат Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Это новый.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//
// Возвращаемое значение:
//
Функция ЭтоНовый(Форма) Экспорт
	Возврат НЕ ЗначениеЗаполнено(Форма.Объект.Ссылка);
КонецФункции

Функция ПодчиненныеЭлементы(ЭлементФормы, Подчиненные = Неопределено) Экспорт

	Если Подчиненные = Неопределено Тогда
		Подчиненные = Новый Массив;
	Иначе
		Подчиненные.Добавить(ЭлементФормы);
	КонецЕсли;

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементФормы, "ПодчиненныеЭлементы") Тогда
		Для каждого ПодчиненныйЭлемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
			ПодчиненныеЭлементы(ПодчиненныйЭлемент, Подчиненные);
		КонецЦикла;
	КонецЕсли;

	Возврат Подчиненные;

КонецФункции

// Пересчет реквизитов формы, указанных в качестве пути в свойстве группы таблицы "ПутьКЗаголовку"
// Применяется только совместно с методом "РаботаСДиалогамиСК.НастроитьТаблицыФормы"
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения
//	ИменаТаблиц - Строка - Имена таблиц для отбора, разделенных ","
//
Процедура ОбновитьЗаголовкиТаблиц(Форма, ИменаТаблиц = Неопределено) Экспорт

	ОтборПоИменамТаблицВключен = ЗначениеЗаполнено(ИменаТаблиц);
	Если ОтборПоИменамТаблицВключен Тогда
		ОтборПоИменамТаблиц = Новый Структура(ИменаТаблиц);
	КонецЕсли;

	Для каждого КлючИЗначение Из Форма.КэшНастройкиТаблиц.РеквизитыИтогов Цикл
		ИмяЭлементаТаблицы = КлючИЗначение.Ключ;
		Если ОтборПоИменамТаблицВключен И НЕ ОтборПоИменамТаблиц.Свойство(ИмяЭлементаТаблицы) Тогда
			Продолжить;
		КонецЕсли;

		ЭлементТаблицы = Форма.Элементы.Найти(ИмяЭлементаТаблицы);
		Если ЭлементТаблицы = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ИмяТаблицы = КлючИЗначение.Значение.ИмяТаблицы;
		ИмяРеквизитаИтога = КлючИЗначение.Значение.ИмяРеквизитаИтога;

		Форма[ИмяРеквизитаИтога] = Форма[ИмяТаблицы].Количество();
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
